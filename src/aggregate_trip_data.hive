CREATE DATABASE IF NOT EXISTS iotdata;

DROP TABLE IF EXISTS iotdata.tripdata;

CREATE EXTERNAL TABLE iotdata.tripdata (
	iotkey string,
	eventDateTime string,
	eventLat float,
	eventLon float,
	eventDist float,
	eventAccMag float,
	tripID string
)
ROW FORMAT DELIMITED FIELDS TERMINATED BY ','
LOCATION '/user/maria_dev/tripdata/'
tblproperties ("skip.header.line.count"="1");

DROP TABLE IF EXISTS iotdata.tripdataInt;

CREATE TABLE iotdata.tripdataInt
(
	iotKey string,
	tripID string,
	starttime string,
	endtime string,
	TotalDistance double,
	cLat double,
	cLon double
);

TRUNCATE TABLE iotdata.tripdataInt;

INSERT INTO TABLE iotdata.tripdataInt
SELECT iotkey,
	tripid,
	MIN(eventdatetime),
	MAX(eventdatetime),
	SUM(eventdist),
	AVG(eventlat),
	AVG(eventlon)
FROM iotdata.tripdata
GROUP BY iotkey,tripid;


DROP TABLE IF EXISTS iotdata.tripdataaccInt;

CREATE TABLE iotdata.tripdataaccInt
(
	iotKey string,
	tripID string,
	accMaxTime string,
	accMax double
);

TRUNCATE TABLE iotdata.tripdataaccInt;

INSERT INTO TABLE iotdata.tripdataaccInt
SELECT iotkey,
	tripid,
	eventdatetime as maxacctime,
	eventaccmag as maxacc
FROM
	(SELECT 
	 	iotkey, 
	 	tripid, 
	 	eventdatetime, 
	 	eventaccmag, 
	 	row_number() over (partition by iotkey,tripid order by eventaccmag DESC) as r
	 FROM iotdata.tripdata) t
WHERE r = 1;

DROP TABLE IF EXISTS iotdata.tripdataFinal;

CREATE TABLE iotdata.tripdataFinal
(
	iotKey string,
	tripID string,
	tripStartTime string,
	tripEndTime string,
	tripDistance double,
	cLat double,
	cLon double,
	tripMaxAcc double,
	tripMaxAccTime string
)ROW FORMAT DELIMITED FIELDS TERMINATED BY ',' LINES TERMINATED BY '\n'
STORED AS TEXTFILE 
LOCATION '/user/maria_dev/tripdata/summary';


TRUNCATE TABLE iotdata.tripdataFinal;

INSERT INTO TABLE iotdata.tripdataFinal
SELECT a.iotkey,
	a.tripid,
	a.starttime,
	a.endtime,
	a.totaldistance,
	a.cLat,
	a.cLon,
	b.accmax,
	b.accmaxtime
FROM iotdata.tripdataint a JOIN iotdata.tripdataaccint b
ON a.tripid = b.tripid;
